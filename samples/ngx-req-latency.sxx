#!/usr/bin/env stap++

global begin_times
global latencies

probe @pfunc(ngx_http_handler) {
    if (pid() == target() && $r == $r->main) {
        begin_times[$r] = gettimeofday_us()
    }
}

probe @pfunc(ngx_http_log_request) {
    if (pid() == target()) {
        begin = begin_times[$r]
        if (begin) {
            latencies <<< gettimeofday_us() - begin
            delete begin_times[$r]
        }
    }
}

probe begin {
    warn(sprintf("Start tracing process %d ($^exec_path)...", target()))
}

probe end {
    printf("Distribution of the main request latencies (in microseconds)\n")
    printf("(min/avg/max: %d/%d/%d\n", @min(latencies), @avg(latencies),
           @max(latencies))
    print(@hist_log(latencies))
}
