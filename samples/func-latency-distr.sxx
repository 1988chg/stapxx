#!/usr/bin/env stap++

# Dump the latency distribution of any function-like probe specified by
# the --arg func=FUNC option.

global begin, stats, func

probe $^arg_func {
    if (pid() == target()) {
        begin = gettimeofday_ns()
        if (func == "") {
            func = probefunc()
        }
    }
}

probe $^arg_func.return {
    if (pid() == target() && begin > 0) {
        stats <<< gettimeofday_ns() - begin
    }
}

probe timer.s($^arg_time :default(5)) {
    exit()
}

probe end {
    if (begin == 0) {
        printf("No samples observed so far.\n");

    } else {
        printf("Distribution of %s latencies (in nanoseconds)\n", func)
        printf("max/avg/min: %d/%d/%d\n", @max(stats), @avg(stats), @min(stats))
        print(@hist_log(stats))
    }
}

probe begin {
    printf("Start tracing %d ($^exec_path)...\nPlease wait for %d seconds.\n\n",
           target(), $^arg_time)
}
